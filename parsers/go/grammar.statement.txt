statement_semi_list: statement_semi+
statement_semi: x=statement pseudo_semi {x}
statement:
    | var_decl
    | const_decl
    | type_decl
    | if_stmt
    | type_switch_stmt
    | expr_switch_stmt
    | select_stmt
    | for_stmt
    | simple_stmt
    | labeled_stmt
    | go_stmt
    | return_stmt
    | break_stmt
    | continue_stmt
    | goto_stmt
    | fallthrough_stmt
    | defer_stmt
    | block

block: '{' x=statement_semi_list? '}' {block_stmt(x)}

label_ident: i=IDENT {label_ident(i)}

fallthrough_stmt: x='fallthrough' {fallthrough_stmt(x,_)}
goto_stmt: 'goto' x=label_ident {goto_stmt(x)}
continue_stmt: 'continue' x=label_ident? {continue_stmt(x)}
break_stmt: 'break' x=label_ident? {break_stmt(x)}

labeled_stmt:
    | x=label_ident ':' b=empty_block {labeled_stmt(x,b)}
    | x=label_ident ':' y=statement {labeled_stmt(x,y)}
    | x=label_ident ':' {labeled_stmt(x,_)}
empty_block: '{' '}' {block_stmt(_)}

defer_stmt: 'defer' x=expression {defer_stmt(x)}
go_stmt: 'go' x=expression {go_stmt(x)}

return_stmt: 'return' x=expression_list? {return_stmt(x)}

simple_stmt:
    | assign_stmt
    | aug_assign_stmt
    | var_decl_stmt
    | inc_stmt
    | dec_stmt
    | send_stmt
    | expression_stmt

inc_stmt: x=expression '++' {inc_stmt(x)}
dec_stmt: x=expression '--' {dec_stmt(x)}
send_stmt: x=expression '<-' y=expression {send_stmt(x,y)}

var_decl_stmt: l=identifier_list ':=' r=expression_list {short_var_decl(l, r)}
assign_stmt: l=expression_list '=' r=expression_list {assign_stmt(l, r)}
aug_assign_stmt: l=expression_list op=aug_op r=expression_list {aug_assign_stmt(l, op, r)}
aug_op: '+=' | '-=' | '|=' | '^=' | '*=' | '/=' | '%=' | '<<=' | '>>=' | '&=' | '&^='
